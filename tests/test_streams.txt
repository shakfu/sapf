;; stream ops tests
1 = throwOnError
[
"ord 4 N [1 2 3 4] equals"
"nat 4 N [0 1 2 3] equals"
"odds 4 N [1 3 5 7] equals"
"evens 4 N [0 2 4 6] equals"
"ints 5 N [0 1 -1 2 -2] equals"
"5 2 by 4 N [5 7 9 11] equals"
"ord sq 4 N [1 4 9 16] equals"
"ord 10 * 5 N [10 20 30 40 50] equals"
"ord +^ 5 N [1 3 5 7 9] equals "
"nat +\ 6 N [0 1 3 6 10 15] equals"
"nat 6 N +/ 15 equals"
"ord 0 1 tog * 8 N [0 2 0 4 0 6 0 8] equals"
"ord 0 tog 8 N [1 0 2 0 3 0 4 0] equals"
"ord 0 1 tog tog 8 N [1 0 2 1 3 0 4 1] equals"
"[1 2 3][4 5 6] $ [1 2 3 4 5 6] equals"
"[[1 2] [[3 [4]]][5]] $/ [1 2 [3 [4]] 5] equals"
"[[[1] [2]] [[3 [4]]][[5]]] $/ $/ [1 2 3 [4] 5] equals"
"ordz 5 N V [1 2 3 4 5] equals"
"ordz 1000 N V ord  1000 N equals"
"ord  1000 N Z ordz 1000 N equals"
"[1 2 3] Z V [1 2 3] equals"
"[1 2 3] Z [1 2 3] equals not"
] aa pr cr
\s [ "Testing : " pr s pr cr
	s compile !
	\[ "Passed" pr cr]
	\[
		"*** FAILED ***" pr cr cr
		throwOnError \[throw] \[] if
	] if
] do
